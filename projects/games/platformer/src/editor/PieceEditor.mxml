<?xml version="1.0" encoding="utf-8"?>
<mx:Application xmlns:mx="http://www.adobe.com/2006/mxml"
    xmlns:editor="editor.*"
    scriptTimeLimit="5"
    layout="absolute" styleName="plain"
    verticalScrollPolicy="off" horizontalScrollPolicy="off"
    width="940" height="800"
    horizontalAlign="center" verticalAlign="middle"
    applicationComplete="loadComplete();">
<mx:Script>
    <![CDATA[
import mx.core.Container;
import mx.controls.TextArea;

import flash.display.Loader;
import flash.net.URLLoader;
import flash.net.URLRequest;

import editor.PieceEditView;

protected function loadComplete() :void
{
    var piecesPath :String = root.loaderInfo.parameters["piecesXML"];
    if (piecesPath == null || piecesPath == "") {
        addEditView();
        return;
    }

    _piecesLoader = new URLLoader();
    _piecesLoader.addEventListener(Event.COMPLETE,
        function (event :Event) :void {
            addEditView();
        });
    _piecesLoader.load(new URLRequest("file://" + piecesPath));
}

protected function addEditView () :void
{
    _codeArea = new TextArea();
    _codeArea.percentWidth = 100;
    _codeArea.percentHeight = 100;
    _codeArea.editable = false;
    _codeArea.setStyle("fontFamily", "Sans");
    _codeArea.setStyle("fontSize", "12");
    xmlCode.addChild(_codeArea);

    var swfPath :String = root.loaderInfo.parameters["piecesSWF"];
    var xmlPieces :XML = (_piecesLoader == null ? null : new XML(_piecesLoader.data));
    pieceEdit.rawChildren.addChild(
            _editView = new PieceEditView(pieceEdit, xmlPieces, swfPath));
}

protected function tabChanged (selected :Container) :void
{
    if (selected == xmlCode) {
        _codeArea.text = _editView.getXML();
    }
}

protected var _codeArea :TextArea;
protected var _editView :PieceEditView;
protected var _piecesLoader :URLLoader;
    ]]>
</mx:Script>
<mx:Panel id="topPanel" title="Platformer Piece Editor" height="100%" width="100%"
    paddingTop="0" paddingLeft="0" paddingRight="0" paddingBottom="0">
    <mx:TabNavigator id="tabs" width="100%" height="100%" change="tabChanged(tabs.selectedChild);">
        <mx:HBox id="editBox" width="100%" label="Edit Piece">
            <editor:FocusContainer id="pieceEdit" width="900" height="700"/>
            <mx:VBox id="iconContainer" width="100%" height="100%" borderStyle="inset">
            </mx:VBox>
        </mx:HBox>
        <mx:VBox id="xmlCode" label="XML Code" width="100%" height="100%"/>
    </mx:TabNavigator>
</mx:Panel>
</mx:Application>
